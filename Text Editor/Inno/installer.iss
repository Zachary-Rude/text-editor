; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Notepad.NET"
#define MyAppVersion "1.2.0"
#define MyAppPublisher "Zach, Inc."
#define MyAppExeName "Text Editor.exe"
#define MyAppAssocName "Text File"
#define MyAppAssocExt ".txt"
#define MyAppAssocKey StringChange(MyAppAssocName, " ", "") + MyAppAssocExt

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{AEAF16D2-ED3B-417F-ABB6-B8A032C7CE2F}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={autopf}\{#MyAppPublisher}\{#MyAppName}
ChangesAssociations=yes
DisableProgramGroupPage=yes
LicenseFile=C:\Users\zacha\Documents\License Agreement.rtf
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir=C:\Users\zacha\source\repos\Text Editor\Text Editor\Inno
OutputBaseFilename=NotepadNETSetup
Compression=lzma
SolidCompression=yes
WizardStyle=classic 
UninstallDisplayIcon={app}\{#MyAppExeName},0
UninstallDisplayName={#MyAppName}
WizardImageFile=compiler:WizClassicImage.bmp 
WizardSmallImageFile=compiler:WizClassicSmallImage.bmp
DisableWelcomePage=no
UsePreviousAppDir=no

[Code]
procedure InitializeWizard();
begin
  { Hide radio buttons and pre-select "accept", to enable "next" button }
  WizardForm.LicenseAcceptedRadio.Checked := True;
  WizardForm.LicenseAcceptedRadio.Visible := False;
  WizardForm.LicenseNotAcceptedRadio.Visible := False;
  WizardForm.LicenseMemo.Height :=
    WizardForm.LicenseNotAcceptedRadio.Top +
    WizardForm.LicenseNotAcceptedRadio.Height -
    WizardForm.LicenseMemo.Top - ScaleY(5);
end;
procedure CurPageChanged(CurPageID: Integer);
begin
  if CurPageID = wpReady then
    WizardForm.NextButton.Caption := SetupMessage(msgButtonInstall)
  else if CurPageID = wpFinished then
    WizardForm.NextButton.Caption := SetupMessage(msgButtonFinish)
  else if CurPageID = wpLicense then
    WizardForm.NextButton.Caption := '&I agree'
  else
    WizardForm.NextButton.Caption := '&Next >';
  WizardForm.BackButton.Caption := '< &Back';
end;

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "txtedtcontext"; Description: "Show Edit with Notepad.NET in Explorer context menu (recommended)"; GroupDescription: "{cm:RegistryEntries}" 

[CustomMessages]
RegistryEntries=Additional Registry entries:

[Files]
Source: "C:\Users\zacha\source\repos\Text Editor\Text Editor\bin\Debug\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Messages]
WelcomeLabel2=Setup will guide you through the installation of {#MyAppName}.%n%nIt is recommended that you close all other applications before starting Setup. This will make it possible to update relevant system files without having to reboot your computer.
ClickNext=Click Next to continue.
SetupWindowTitle=%1 Setup
SetupAppTitle=%1 Setup  
BeveledLabel=Powered by Inno Setup

[Registry]
Root: HKCR; Subkey: "{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; ValueData: ""; Flags: uninsdeletevalue
Root: HKCR; Subkey: "{#MyAppAssocKey}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppAssocName}"; Flags: uninsdeletekey
Root: HKCR; Subkey: "{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "C:\Windows\System32\imageres.dll,246"
Root: HKCR; Subkey: "{#MyAppAssocKey}\shell\open"; ValueType: string; ValueName: ""; ValueData: "Edit"
Root: HKCR; Subkey: "{#MyAppAssocKey}\shell\open"; ValueType: string; ValueName: "Icon"; ValueData: "{app}\{#MyAppExeName},0"
Root: HKCR; Subkey: "{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""
Root: HKCR; Subkey: "*\shell\TXTEdit"; ValueType: string; ValueName: ""; ValueData: "Edit with {#MyAppName}"; Tasks: txtedtcontext; Flags: uninsdeletekey
Root: HKCR; Subkey: "*\shell\TXTEdit"; ValueType: string; ValueName: "Icon"; ValueData: "{app}\{#MyAppExeName},0"; Tasks: txtedtcontext
Root: HKCR; Subkey: "*\shell\TXTEdit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""; Tasks: txtedtcontext
Root: HKCR; Subkey: "Applications\{#MyAppExeName}\SupportedTypes"; ValueType: string; ValueName: ".txt"; ValueData: ""

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

