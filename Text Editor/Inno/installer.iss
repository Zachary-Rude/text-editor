; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Notepad.NET"
#define MyAppVersion "1.2.0"
#define MyAppPublisher "Zach, Inc."
#define MyAppExeName "Notepad.NET.exe"
#define MyAppAssocName "Text File"
#define MyAppAssocExt ".txt"
#define MyAppAssocKey StringChange(MyAppName, ".", "") + MyAppAssocExt

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{AEAF16D2-ED3B-417F-ABB6-B8A032C7CE2F}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={autopf}\{#MyAppPublisher}\{#MyAppName}
ChangesAssociations=yes
DisableProgramGroupPage=yes
LicenseFile=C:\Users\zacha\Documents\License Agreement.rtf
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir=.
OutputBaseFilename=NotepadNETSetup
Compression=lzma
SolidCompression=yes
WizardStyle=classic 
UninstallDisplayIcon={app}\{#MyAppExeName},0
UninstallDisplayName={#MyAppName}
SetupIconFile=..\Resources\icon-72.ico
WizardImageFile=compiler:WizClassicImage.bmp 
WizardSmallImageFile=compiler:WizClassicSmallImage.bmp
DisableWelcomePage=no
UsePreviousAppDir=no
ArchitecturesAllowed=x64
ArchitecturesInstallIn64BitMode=x64

[Code]
procedure InitializeWizard();
begin
  { Hide radio buttons and pre-select "accept", to enable "next" button }
  WizardForm.LicenseAcceptedRadio.Checked := True;
  WizardForm.LicenseAcceptedRadio.Visible := False;
  WizardForm.LicenseNotAcceptedRadio.Visible := False;
  WizardForm.LicenseMemo.Height :=
    WizardForm.LicenseNotAcceptedRadio.Top +
    WizardForm.LicenseNotAcceptedRadio.Height -
    WizardForm.LicenseMemo.Top - ScaleY(5);
end;
procedure CurPageChanged(CurPageID: Integer);
begin
  if CurPageID = wpReady then
    WizardForm.NextButton.Caption := SetupMessage(msgButtonInstall)
  else if CurPageID = wpFinished then
    WizardForm.NextButton.Caption := SetupMessage(msgButtonFinish)
  else if CurPageID = wpLicense then
    WizardForm.NextButton.Caption := '&I agree'
  else
    WizardForm.NextButton.Caption := '&Next >';
  WizardForm.BackButton.Caption := '< &Back';
end;

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "Create a &desktop icon"; GroupDescription: "Additional icons:"; Flags: unchecked
Name: "npdneditcontext"; Description: "{cm:AddContextMenuFiles,{#MyAppName}}"; GroupDescription: "{cm:Other}"
Name: "associate"; Description: "Associate Notepad.NET with text files"; GroupDescription: "{cm:Other}"

[Files]
Source: "..\bin\x64\Release\*"; Excludes: "*.pdb"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Messages]
SetupWindowTitle=%1 Setup
SetupAppTitle=%1 Setup

[CustomMessages]
AddContextMenuFiles=Add "Edit with %1" action to File Explorer context menu
Other=Other:

[Registry]
Root: HKCR; Subkey: "{#MyAppAssocExt}\OpenWithProgids"; ValueType: string; ValueName: "{#MyAppAssocKey}"; ValueData: ""; Tasks: associate; Flags: uninsdeletevalue
Root: HKCR; Subkey: "{#MyAppAssocKey}"; ValueType: string; ValueName: ""; ValueData: "{#MyAppAssocName}"; Tasks: associate; Flags: uninsdeletekey
Root: HKCR; Subkey: "{#MyAppAssocKey}\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "C:\Windows\System32\imageres.dll,246"; Tasks: associate
Root: HKCR; Subkey: "{#MyAppAssocKey}\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""; Tasks: associate
Root: HKCR; Subkey: "*\shell\Edit with {#MyAppName}"; ValueType: string; ValueName: "Icon"; ValueData: "{app}\{#MyAppExeName},0"; Tasks: npdneditcontext; Flags: uninsdeletekey
Root: HKCR; Subkey: "*\shell\Edit with {#MyAppName}\command"; ValueType: string; ValueName: ""; ValueData: """{app}\{#MyAppExeName}"" ""%1"""; Tasks: npdneditcontext
Root: HKCR; Subkey: "Applications\{#MyAppExeName}\SupportedTypes"; ValueType: string; ValueName: ".txt"; ValueData: ""; Tasks: associate; Flags: uninsdeletekey 

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

